# SUSE's openQA tests
#
# Copyright 2021-2025 SUSE LLC
# SPDX-License-Identifier: FSFAP

# Summary: Test rootless mode on podman.
# - add a user on the /etc/subuid and /etc/subgid to allow automatically allocation subuid and subgid ranges.
# - check uids allocated to user (inside the container are mapped on the host)
# - give read access to the SUSE Customer Center credentials to call zypper from in the container.
#   This grants the current user the required access rights
# - Test rootless container:
#   * container is launched with default root user
#   * container is launched with existing user id
#   * container is launched with keep-id of the user who run the container
# - Restore /etc/zypp/credentials.d/ credentials
# Maintainer: QE-C team <qa-c@suse.de>

use Mojo::Base 'containers::basetest';
use testapi;
use serial_terminal;
use utils;
use containers::common;
use containers::container_images;
use version_utils qw(is_sle is_leap is_jeos is_transactional package_version_cmp is_tumbleweed);
use Utils::Architectures;
use Utils::Logging 'save_and_upload_log';

sub run {
    my ($self) = @_;
    select_serial_terminal;
    my $user = $testapi::username;

    # Workaround for https://progress.opensuse.org/issues/186834
    script_run "touch /etc/SUSEConnect" unless (is_sle(">=16") || is_tumbleweed);

    my $podman = $self->containers_factory('podman');

    # add testuser to systemd-journal group to allow non-root
    # user to access container logs via journald event driver
    # bsc#1207673, bsc#1218023
    assert_script_run("usermod -aG systemd-journal $user") if (is_leap("<16") || is_sle("<16"));

    if (get_var('TDUP')) {
        my $cont_storage = '/etc/containers/storage.conf';
        my $local_storage = "/home/$user/.local";

        # use only packaged storage config in /usr/share/containers/storage.conf
        if (script_run("test -f $cont_storage &> /dev/null") == 0) {
            assert_script_run("rm -rf $cont_storage");
            assert_script_run('rm -rf /var/lib/containers/storage');
        }

        # prevent database graph driver mismatch
        if (script_run("test -d $local_storage &> /dev/null") == 0) {
            record_soft_failure('gh#containers/podman#24738');
            assert_script_run("rm -rf $local_storage");
        }

        assert_script_run('podman system reset -f');
    }

    # Prepare for Podman 3.4.4 and CGroups v2
    if ((is_sle('15-SP3+') || is_leap('15.3+')) && !check_var('CONTAINERS_CGROUP_VERSION', '1')) {
        switch_cgroup_version($self, 2);
        select_serial_terminal;

        validate_script_output('podman info', sub { /cgroupVersion: v2/ }, fail_message => "podman not operating in cgroup v2");
    }

    # Check for bsc#1192051
    # Test needs to pass, if seccomp filtering is off
    my $image = get_var("CONTAINER_IMAGE_TO_TEST", "registry.opensuse.org/opensuse/tumbleweed:latest");
    assert_script_run("podman run --security-opt=seccomp=unconfined --rm -it $image bash -c 'test -x /bin/sh'");
    # And this one is the actual check for bsc#1192051, with seccomp filtering on
    assert_script_run("podman run --rm -it $image bash -c 'test -x /bin/sh'", fail_message => "bsc#1192051 - Permission denied for faccessat2");

    # Some products don't have bernhard pre-defined (e.g. SLE Micro)
    if (script_run("grep $user /etc/passwd") != 0) {
        assert_script_run "useradd -m $user";
        assert_script_run "echo '$user:$testapi::password' | chpasswd";
        # Make sure user has access to tty group
        my $serial_group = script_output "stat -c %G /dev/$testapi::serialdev";
        assert_script_run "grep '^${serial_group}:.*:${user}\$' /etc/group || (chown $user /dev/$testapi::serialdev && gpasswd -a $user $serial_group)";
    }

    # NOTE: Remove this hack when 15-SP3 is EOL
    my $subuid_start = get_user_subuid($user);
    if ($subuid_start eq '') {
        $subuid_start = 200000;
        my $subuid_range = $subuid_start + 65535;
        assert_script_run "usermod --add-subuids $subuid_start-$subuid_range --add-subgids $subuid_start-$subuid_range $user";
    }
    assert_script_run "grep $user /etc/subuid", fail_message => "subuid range not assigned for $user";
    assert_script_run "grep $user /etc/subgid", fail_message => "subgid range not assigned for $user";
    assert_script_run "setfacl -m u:$user:r /etc/zypp/credentials.d/*" if is_sle;

    # Remove all previous commands generated by root. Some of these commands will be triggered
    # by the rootless user and will generate the same file /tmp/scriptX which will fail if it
    # already exists owned by root
    assert_script_run 'rm -rf /tmp/script*';
    ensure_serialdev_permissions;
    if (is_transactional) {
        select_console "user-console";
    } else {
        select_user_serial_terminal();
    }

    # By default the storage driver is set to btrfs if /var is in btrfs
    # but if the home partition is not btrfs podman commands will fail with
    # Error: "/home/bernhard/.local/share/containers/storage/btrfs" is not on a btrfs filesystem
    my $storage = $podman->get_storage_driver();
    if ($storage ne 'overlay') {
        die "Unexpected storage driver -> $storage";
    }
    record_info("podman info", script_output("podman info"));

    test_container_image(image => $image, runtime => $podman);
    build_and_run_image(base => $image, runtime => $podman);
    test_zypper_on_container($podman, $image);
    verify_userid_on_container($image, $subuid_start);
    $podman->cleanup_system_host();

    # Like above, but the other way around: Delete the files left by the regular user.
    assert_script_run 'rm -rf /tmp/script*';
}

sub get_user_subuid {
    my ($user) = shift;
    my $start_range = script_output("awk -F':' '\$1 == \"$user\" {print \$2}' /etc/subuid",
        proceed_on_failure => 1);
    return $start_range;
}

sub verify_userid_on_container {
    my ($image, $start_id) = @_;
    my $huser_id = script_output "echo \$UID";
    record_info "host uid", "$huser_id";
    record_info "root default user", "rootless mode process runs with the default container user(root)";
    my $cid = script_output "podman run -d --rm --name test1 $image sleep infinity 2>/dev/null";
    validate_script_output "podman top $cid user huser", sub { /root\s+1000/ };
    validate_script_output "podman top $cid capeff", sub { /setuid/i };

    record_info "non-root user", "process runs under the range of subuids assigned for regular user";
    $cid = script_output "podman run -d --rm --name test2 --user 1000 $image sleep infinity 2>/dev/null";
    my $id = $start_id + $huser_id - 1;

    # podman >= v4.4.0 lists username instead of uid
    # when using `user` descriptor with `podman top`, handle
    # conditionally.
    # https://github.com/os-autoinst/os-autoinst-distri-opensuse/pull/16567
    my $huser_name = $testapi::username;
    validate_script_output "podman top $cid user huser", sub { /${huser_name}\s+${id}|1000\s+${id}/ };
    validate_script_output "podman top $cid capeff", sub { /none/ };

    record_info "root with keep-id", "the default user(root) starts process with the same uid as host user";
    $cid = script_output "podman run -d --rm --userns keep-id $image sleep infinity 2>/dev/null";
    # Remove once the softfail removed. it is just checks the user's mapped uid
    validate_script_output "podman exec -it $cid cat /proc/self/uid_map", sub { /1000/ };
    # Check for bsc#1182428
    my $exp_user = $testapi::username;
    validate_script_output "podman top $cid user huser", sub { /$exp_user\s+$exp_user/ };
    validate_script_output("podman top $cid capeff", sub { /none/ }, fail_message => "Container should not have any capabilities");

    ## Check if uid change within the container works as desired
    # Note: If this part with 'zypper install' becomes cumbersome we could switch to an image, which already includes sudo and useradd
    my $cmd = '(id | grep uid=0) && zypper -n -q in sudo shadow && useradd geeko -u 1000 && (sudo -u geeko id | grep geeko)';
    script_retry("podman run -ti --rm '$image' bash -c '$cmd'", timeout => 300, retry => 3, delay => 60);

}

sub post_run_hook {
    my $self = shift;
    select_serial_terminal();
    assert_script_run "setfacl -x u:$testapi::username /etc/zypp/credentials.d/*" if is_sle;
    $self->SUPER::post_run_hook;
}

sub post_fail_hook {
    my $self = shift;
    save_and_upload_log('cat /etc/{subuid,subgid}', "/tmp/permissions.txt");
    if (is_sle) {
        save_and_upload_log('ls -la /etc/zypp/credentials.d', "/tmp/credentials.d.perm.txt");
        assert_script_run "setfacl -x u:$testapi::username /etc/zypp/credentials.d/*";
    }
    $self->SUPER::post_fail_hook;
}

1;
