# SUSE's openQA tests
#
# Copyright 2018 SUSE LLC
# SPDX-License-Identifier: FSFAP

# Package: openvpn easy-rsa iputils
# Summary: Test OpenVPN on two machines - this one is the server.
#  * Shared key is generated, server configured and started
#  * After client connects, both sides perform ping, then disconnect
#  * Easy-RSA CA infrastructure is generated, server configured and started
#  * After client connects, both sides perform ping, then disconnect
# Maintainer: QE Security <none@suse.de>

use base 'consoletest';
use testapi;
use serial_terminal 'select_serial_terminal';
use lockapi;
use y2_module_guitest;
use mm_network;
use mmapi 'wait_for_children';
use utils qw(systemctl zypper_call exec_and_insert_password script_retry);
use version_utils qw(is_sle is_opensuse);
use repo_tools 'add_qa_head_repo';
use registration qw(add_suseconnect_product get_addon_fullname is_phub_ready);
use Utils::Architectures;
use network_utils 'iface';

sub run {
    my $server_ip = get_var('SERVER_IP', '10.0.2.101');
    my $client_ip = get_var('CLIENT_IP', '10.0.2.102');
    barrier_create 'SETUP_DONE', 2;
    barrier_create('OPENVPN_STATIC_STARTED', 2);
    barrier_create('OPENVPN_STATIC_FINISHED', 2);
    barrier_create('OPENVPN_CA_STARTED', 2);
    barrier_create('OPENVPN_CA_FINISHED', 2);
    mutex_create 'barrier_setup_done';
    barrier_wait 'SETUP_DONE';
    select_serial_terminal;

    # Install runtime dependencies
    zypper_call("in iputils");

    # We don't run setup_multimachine in s390x, but we need to know the server and client's
    # ip address, so we add a known ip to NETDEV
    my $netdev = iface();
    assert_script_run("ip addr add $server_ip/24 dev $netdev") if (is_s390x);
    assert_script_run('systemctl restart sshd');

    # Install openvpn, generate static key. On openSUSE or SLE>=15-SP2 we have a newer version in PHub compared to qa_head.
    # SLE16 has no easy-rsa.
    add_qa_head_repo unless (is_opensuse() || (is_sle('>=15-SP2') && !is_sle('>=16.0')));
    add_suseconnect_product(get_addon_fullname('phub')) if (is_phub_ready() && !is_sle('>=16.0'));

    zypper_call('--gpg-auto-import-keys in openvpn easy-rsa');
    zypper_call("install openssl") if (script_run("which openssl") != 0);
    assert_script_run('cd /etc/openvpn');
    assert_script_run('openvpn --genkey --secret static.key');
    mutex_create 'OPENVPN_STATIC_KEY';

    # Download the server config
    assert_script_run('curl -o static.conf ' . data_url('openvpn/static_server.conf'));

    # Remove unsupported configuration options on older SLE versions
    assert_script_run('sed -i "/^cipher/d; /^data-ciphers/d" static.conf') if (is_sle('<15-sp4'));

    # Send client key
    if (is_s390x) {
        assert_script_run 'firewall-cmd --zone=public --permanent --add-port=8008/tcp';
        assert_script_run 'firewall-cmd --zone=public --permanent --add-port=1194/udp';
        assert_script_run 'firewall-cmd --reload';
    }
    my $server_pid = background_script_run "python3 -m http.server -b $server_ip 8008";

    # Start the server
    systemctl('start openvpn@static');
    systemctl('status openvpn@static -l');

    # Make sure the tunnel has been established
    script_retry('journalctl --no-pager -u openvpn@static | grep "Initialization Sequence Completed"', delay => 15, retry => 12);

    # Test that the interface is present
    assert_script_run("ip a s tun0");
    assert_script_run('ip a s tun0 | grep "10\.8\.0\.1"');

    # Test the connection when also the client is ready
    barrier_wait 'OPENVPN_STATIC_STARTED';
    assert_script_run("ping -c5 -W1 -I tun0 10.8.0.2");

    # Stop the server when also client is done
    barrier_wait 'OPENVPN_STATIC_FINISHED';
    systemctl('stop openvpn@static');

    # Generate certificates
    assert_script_run("easyrsa init-pki");
    # On SLE 15 SP6 and newer, DH parameters generated by Easy-RSA are no longer accepted due to incompatibility with the updated FIPS 186-5 requirements.
    my $dh_params_cmd = is_sle('<15-SP6') ? "easyrsa gen-dh" : "openssl genpkey  -genparam -algorithm DH -out pki/dh.pem -pkeyopt group:ffdhe2048";
    assert_script_run("$dh_params_cmd", 600);
    script_run("yes '' | easyrsa build-ca nopass", 120);
    script_run("yes '' | easyrsa gen-req server nopass", 120);
    assert_script_run("echo 'yes' | easyrsa sign-req server server", 120);
    script_run("yes '' | easyrsa gen-req client nopass", 120);
    assert_script_run("echo 'yes' | easyrsa sign-req client client", 120);
    mutex_create 'OPENVPN_CA_KEYS';

    # Download the server config
    assert_script_run('curl -o ca.conf ' . data_url('openvpn/ca_server.conf'));

    # Remove unsupported configuration options on older SLE versions
    assert_script_run('sed -i "/^cipher/d; /^data-ciphers/d" ca.conf') if (is_sle('<15-sp4'));

    # Create the client config directory and the file for client
    assert_script_run("mkdir ccd");
    assert_script_run(qq(echo "ifconfig-push 10.8.0.2 255.255.255.0" > ccd/client));

    # Start the server
    systemctl('start openvpn@ca');
    systemctl('status openvpn@ca -l');

    # Make sure the tunnel has been established
    script_retry('journalctl --no-pager -u openvpn@ca | grep "Initialization Sequence Completed"', delay => 15, retry => 12);

    # Test that the interface is present
    assert_script_run("ip a s tap0");
    assert_script_run('ip a s tap0 | grep "10\.8\.0\.1"');

    # Test the connection when also the client is ready
    barrier_wait 'OPENVPN_CA_STARTED';
    assert_script_run("ping -c5 -W1 -I tap0 10.8.0.2");

    # Stop the server when also client is done
    barrier_wait 'OPENVPN_CA_FINISHED';
    systemctl('stop openvpn@ca');

    wait_for_children();

    # Delete the ip that we added if arch is s390x
    assert_script_run("ip addr del $server_ip/24 dev $netdev") if (is_s390x);
}

1;
