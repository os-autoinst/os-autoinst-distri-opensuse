# SUSE's openQA tests
#
# Copyright SUSE LLC
# SPDX-License-Identifier: FSFAP
# Maintainer: QE-SAP <qe-sap@suse.de>
# Summary: Library sub and shared data for the ipaddr2 cloud test.

package sles4sap::ipaddr2;

use strict;
use warnings FATAL => 'all';
use Mojo::Base -signatures;
use Carp qw( croak );
use Exporter qw(import);
use Mojo::JSON qw( decode_json );
# Intentionally not use NetAddr::IP as it result in module to try
# a Fully Qualified Domain Name which returns an ipV4 address or an ipV6 address
# embodied in that order. This feature can be disabled with:
use NetAddr::IP::Lite ':nofqdn';
use testapi;
use mmapi qw( get_current_job_id );
use utils qw( write_sut_file ssh_fully_patch_system);
use hacluster qw($crm_mon_cmd cluster_status_matches_regex);
use publiccloud::utils qw( get_ssh_private_key_path register_addon);
use sles4sap::ibsm;
use sles4sap::azure_cli;


=head1 SYNOPSIS

Library to manage ipaddr2 tests
=cut

our @EXPORT = qw(
  ipaddr2_infra_deploy
  ipaddr2_infra_destroy
  ipaddr2_configure_web_server
  ipaddr2_repo_refresh
  ipaddr2_repo_list
  ipaddr2_deployment_sanity
  ipaddr2_deployment_logs
  ipaddr2_os_sanity
  ipaddr2_os_connectivity_sanity
  ipaddr2_cluster_create
  ipaddr2_cluster_sanity
  ipaddr2_cluster_check_version
  ipaddr2_bastion_key_accept
  ipaddr2_bastion_pubip
  ipaddr2_bastion_ssh_addr
  ipaddr2_internal_key_accept
  ipaddr2_internal_key_gen
  ipaddr2_scc_check
  ipaddr2_scc_register
  ipaddr2_scc_addons
  ipaddr2_crm_move
  ipaddr2_crm_clear
  ipaddr2_wait_for_takeover
  ipaddr2_test_master_vm
  ipaddr2_test_other_vm
  ipaddr2_cloudinit_create
  ipaddr2_logs_cloudinit
  ipaddr2_azure_resource_group
  ipaddr2_network_peering_create
  ipaddr2_patch_system
  ipaddr2_repos_add_server_to_hosts
  ipaddr2_cleanup
  ipaddr2_logs_collect
  ipaddr2_ssh_intrusion_detection
);

use constant DEPLOY_PREFIX => 'ip2t';
use constant WEB_RSC => 'rsc_web_00';
use constant USER => 'cloudadmin';
use constant SSH_KEY_ID => 'id_rsa';
use constant SSH_VERBOSE => '-vvv';
use constant SSH_LOG => '/var/tmp/ssh_sut.log.txt';
use constant SSH_PROXY_LOG => '/var/tmp/ssh_proxy_sut.log.txt';
use constant PING_CMD => 'ping -c 3';

our $bastion_vm_name = DEPLOY_PREFIX . "-vm-bastion";
our $bastion_pub_ip = DEPLOY_PREFIX . '-pub_ip';
our $nat_pub_ip = DEPLOY_PREFIX . '-nat_pub_ip';
# Storage account name must be between 3 and 24 characters in length
# and use numbers and lowercase letters only.
our $storage_account = DEPLOY_PREFIX . 'storageaccount';
our %priv_net_address_range = get_private_ip_range();
our $priv_ip_range = $priv_net_address_range{priv_ip_range};
our $frontend_ip = $priv_ip_range . '.50';

=head2 get_private_ip_range

    my $range = get_private_ip_range();

count the private ip range and return
=cut

sub get_private_ip_range {
    my %range = (main_address_range => '192.168.0.0/16', subnet_address_range => '192.168.0.0/24');
    if (my $worker_id = get_var("WORKER_ID")) {
        %range = ibsm_calculate_address_range(slot => $worker_id);
    }

    $range{priv_ip_range} = ($range{main_address_range} =~ /^(\d+\.\d+\.\d+)\./) ? $1 : '';

    return %range;
}

=head2 ipaddr2_azure_resource_group

    my $rg = ipaddr2_azure_resource_group();

Get the Azure resource group name for this test
=cut

sub ipaddr2_azure_resource_group {
    return DEPLOY_PREFIX . get_current_job_id();
}

=head2 ipaddr2_cloudinit_create

    my $cloud_init_file = ipaddr2_cloudinit_create();

Create the cloud-init.txt profile file

=over

=item B<scc_code> - Optional argument. If present, registration will be added in bootcmd 
                    section of the cloud-init profile file.
                    Having registration enabled is particularly important for BYOS images:
                    this profile also try to install nginx, this stage needs a previous registration.

=item B<nginx_root> - Optional argument: default is B</srv/www/htdocs>. This argument allow
                      to overwrite the location where the index.html, generated by the test and used to identify each node,
                      is stored. As the test is not using any nginx configuration file, this location has to match to the
                      default root folder configured by the particular version of nginx installed in the SUT.
                      Default value is/was value for nginx installable in 15sp5 and sp6.
                      12sp5 needs /usr/share/nginx/html

=item B<external_repo> - Optional argument: allow to add a PackageHub product

=back
=cut

sub ipaddr2_cloudinit_create(%args) {
    $args{nginx_root} //= '/srv/www/htdocs';

    # start to dynamically compose the profile
    # 1. Header
    # 2. disable initial zypper up, the update is done later by a dedicated test module,
    #    after adding repo for package we like to test
    # 3. socat is needed but not installed by default in 12sp5
    my $cloud_init_content = <<END;
#cloud-config
package_upgrade: false
packages:
  - socat
END
    # 4. Install and configure nginx, two variants as
    #    12sp5 does not have the nginx package
    if ($args{external_repo}) {
        $cloud_init_content .= <<END;
runcmd:
  - 'sudo SUSEConnect --product PackageHub/$args{external_repo}/x86_64'
  - 'sudo zypper ref'
  - 'sudo zypper install -y nginx'
END
    } else {
        $cloud_init_content .= <<END;
  - nginx
runcmd:
END
    }
    $cloud_init_content .= <<END;
  - 'echo "I am \$(hostname)" > $args{nginx_root}/index.html'
END
    # 3. Optionally add registration
    if ($args{scc_code}) {
        $cloud_init_content .= <<END;
bootcmd:
  - registercloudguest --clean
  - registercloudguest --force-new -r $args{scc_code}
END
    }
    my $cloud_init_file = '/tmp/cloud-init-web.txt';
    write_sut_file($cloud_init_file, $cloud_init_content);
    # upload to allow debugging
    upload_logs($cloud_init_file);
    return $cloud_init_file;
}

=head2 ipaddr2_infra_deploy

    my $rg = ipaddr2_infra_deploy();

Create a deployment in Azure designed for this specific test.

1. Create a resource group to contain all
2. Create a vnet and subnet in it
3. Create one Public IP
4. Create 2 VM to run the cluster, both running a web server and that are behind the LB
5. Create 1 additional VM that get
6. Create a Load Balancer with 2 VM in backend and with an IP as frontend

=over

=item B<region> - existing resource group

=item B<os> - existing Load balancer NAME

=item B<diagnostic> - enable diagnostic features if 1

=item B<cloudinit_profile> - filename of the cloud-init profile. Usually this one is the return value of
                      B<ipaddr2_cloudinit_create>.
                      Presence of this optional argument also enable cloud-init features in various deployment stages.

=item B<trusted_launch> - Enable or disable Trusted Launch. Default 1: Enabled.
                          If configured to 0 the result in az vm create is executed with '--security-type Standard'

=back
=cut

sub ipaddr2_infra_deploy(%args) {
    foreach (qw(region os)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{diagnostic} //= 0;
    $args{trusted_launch} //= 1;

    az_version();

    my $rg = ipaddr2_azure_resource_group();

    az_group_create(
        name => $rg,
        region => $args{region});

    # If image provided is a blob storage link, create image out of it
    if ($args{os} =~ /\.vhd$/) {
        my $img_name = $rg . 'img';
        az_img_from_vhd_create(
            resource_group => $rg,
            name => $img_name,
            source => $args{os});
        $args{os} = $img_name;
    }

    # Create a VNET only needed later when creating the VM
    # Use $rg instead of DEPLOY_PREFIX to try to prevent
    # certain deployment failures like:
    #    Subnet(ip2t-snet) does not exist, but failed to create a new subnet
    #    with address prefix 10.0.0.0/24.
    my $vnet = "$rg-vnet";
    my $subnet = "$rg-snet";
    az_network_vnet_create(
        resource_group => $rg,
        region => $args{region},
        vnet => $vnet,
        address_prefixes => $priv_net_address_range{main_address_range},
        snet => $subnet,
        subnet_prefixes => $priv_net_address_range{subnet_address_range});

    # Create a Network Security Group
    # only needed later when creating the VM
    my $nsg = DEPLOY_PREFIX . '-nsg';
    az_network_nsg_create(
        resource_group => $rg,
        name => $nsg);

    # Create a public IP for external test access.
    # It is later assigned to the third VM (bastion role)
    az_network_publicip_create(
        resource_group => $rg,
        name => $bastion_pub_ip,
        sku => 'Standard',
        allocation_method => 'Static');

    # Create a public IP for the NAT Gateway
    az_network_publicip_create(
        resource_group => $rg,
        name => $nat_pub_ip,
        sku => 'Standard',
        allocation_method => 'Static');

    # Create the NAT Gateway
    my $nat_name = DEPLOY_PREFIX . '-nat';
    az_network_nat_gateway_create(
        resource_group => $rg,
        region => $args{region},
        name => $nat_name,
        public_ip => $nat_pub_ip);

    # Associate one of the Public IP to the NAT Gateway
    az_network_vnet_subnet_update(
        resource_group => $rg,
        vnet => $vnet,
        snet => $subnet,
        nat_gateway => $nat_name);

    # Create the load balancer entity.
    # Think about it as a "group" definition
    # to link back-end (2 VMs) and front-end (the Pub IP) resources
    # SKU Standard (and not Basic) is needed to get some Metrics
    my $lb = DEPLOY_PREFIX . '-lb';
    my $lb_be = DEPLOY_PREFIX . '-backend_pool';
    my $lb_fe = DEPLOY_PREFIX . '-frontend_ip';
    az_network_lb_create(
        resource_group => $rg,
        name => $lb,
        vnet => $vnet,
        snet => $subnet,
        backend => $lb_be,
        frontend_ip_name => $lb_fe,
        fip => $frontend_ip,
        sku => 'Standard');

    # All the 2 VM are later assigned to it.
    # The load balancer does not explicitly knows about it
    my $as = DEPLOY_PREFIX . '-as';
    az_vm_as_create(
        resource_group => $rg,
        name => $as,
        region => $args{region},
        fault_count => 2);
    # Next two lines are for debug purpose only:
    # in time to time next vm create fails for missing AS
    az_vm_as_list(resource_group => $rg);
    az_vm_as_show(
        resource_group => $rg,
        name => $as);

    my $storage_name;
    if ($args{diagnostic}) {
        $storage_name = $storage_account . get_current_job_id();
        az_storage_account_create(
            resource_group => $rg,
            region => $args{region},
            name => $storage_name);
    }

    # - Create 2 VMs
    # - for each of them open port 80
    # - link their NIC/ipconfigs to the load balancer to be managed
    my $vm;
    my %vm_create_generic_args = (
        resource_group => $rg,
        region => $args{region},
        image => $args{os},
        username => USER,
        vnet => $vnet,
        snet => $subnet,
        ssh_pubkey => get_ssh_private_key_path() . '.pub',
        public_ip => "");
    if (!$args{trusted_launch}) {
        $vm_create_generic_args{security_type} = 'Standard';
    }

    my %vm_create_internal_args = %vm_create_generic_args;
    $vm_create_internal_args{availability_set} = $as;
    $vm_create_internal_args{nsg} = $nsg;
    $vm_create_internal_args{public_ip} = "";
    $vm_create_internal_args{custom_data} = $args{cloudinit_profile} if ($args{cloudinit_profile});

    foreach my $i (1 .. 2) {
        $vm = ipaddr2_get_internal_vm_name(id => $i);
        # the VM creation command refers to an external cloud-init
        # configuration file that is in charge to install and setup
        # the nginx server.
        $vm_create_internal_args{name} = $vm;
        az_vm_create(%vm_create_internal_args);

        if ($args{diagnostic}) {
            az_vm_diagnostic_log_enable(resource_group => $rg,
                storage_account => $storage_name,
                vm_name => $vm);
        }

        if ($args{cloudinit_profile}) {
            az_vm_wait_cloudinit(
                resource_group => $rg,
                name => $vm,
                username => USER);
        }

        az_vm_openport(
            resource_group => $rg,
            name => $vm, port => 80);
    }

    my %vm_create_bastion_args = %vm_create_generic_args;
    $vm_create_bastion_args{name} = $bastion_vm_name;
    $vm_create_bastion_args{public_ip} = $bastion_pub_ip;
    az_vm_create(%vm_create_bastion_args);

    # Keep this loop separated from the other to hopefully
    # give cloud-init more time to run and avoid interfering
    # with it by changing the networking on the running VM
    foreach my $i (1 .. 2) {
        my $vm = ipaddr2_get_internal_vm_name(id => $i);
        my $nic_id = az_nic_get_id(
            resource_group => $rg,
            name => $vm);
        my $ip_config = az_ipconfig_name_get(nic_id => $nic_id);
        my $nic_name = az_nic_name_get(nic_id => $nic_id);

        # Change the IpConfig to use a static IP:
        # https://documentation.suse.com/sle-ha/15-SP5/html/SLE-HA-all/article-installation.html#vl-ha-inst-quick-req-other
        az_ipconfig_update(
            resource_group => $rg,
            ipconfig_name => $ip_config,
            nic_name => $nic_name,
            ip => ipaddr2_get_internal_vm_private_ip(id => $i));

        # Add the IpConfig to the LB pool
        az_ipconfig_pool_add(
            resource_group => $rg,
            lb_name => $lb,
            address_pool => $lb_be,
            ipconfig_name => $ip_config,
            nic_name => $nic_name,
            timeout => 300);
    }

    # Health probe is using the port exposed by the cluster RA azure-lb
    # to understand if each of the VM in the cluster is OK.
    # It is eventually the cluster itself that
    # cares to monitor the below service (port 80)
    my $lb_health_port_name = $lb . "_health";
    my $lb_health_port = '62500';
    az_network_lb_probe_create(
        resource_group => $rg,
        lb_name => $lb,
        name => $lb_health_port_name,
        port => $lb_health_port);

    # Configure the load balancer behavior
    az_network_lb_rule_create(
        resource_group => $rg,
        lb_name => $lb,
        hp_name => $lb_health_port_name,
        backend => $lb_be,
        frontend_ip => $lb_fe,
        name => $lb . "_rule",
        port => '80');

    foreach (1 .. 2) {
        my $wt = az_vm_wait_running(
            resource_group => $rg,
            name => ipaddr2_get_internal_vm_name(id => $_),
            timeout => 1200
        );
        record_info('VM RUNNING', "VM $_ takes $wt seconds to reach the Running state.");
    }
}

=head2 ipaddr2_bastion_pubip

    my $bastion_ip = ipaddr2_bastion_pubip();

Get the only public IP in the deployment associated to the VM used as bastion. Function is getting
the IP address using az cli, so it could die if the az cli fails to return a valid IP.
=cut

sub ipaddr2_bastion_pubip {
    my $rg = ipaddr2_azure_resource_group();
    my $pub_ip = az_network_publicip_get(resource_group => $rg, name => $bastion_pub_ip);
    my $ip_obj = NetAddr::IP::Lite->new($pub_ip) or die "Invalid IP '$pub_ip'";
    return $ip_obj->addr;
}

=head2 ipaddr2_bastion_ssh_addr

    script_run(join(' ', 'ssh', ipaddr2_bastion_ssh_addr(), 'whoami');

Help to create ssh command that target the only VM
in the deployment that has public IP.

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_bastion_ssh_addr(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    return USER . '@' . $args{bastion_ip};
}

=head2 ipaddr2_bastion_key_accept

    ipaddr2_bastion_key_accept()

For the worker to accept the ssh key of the bastion

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_bastion_key_accept(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    # Clean up known_hosts on the machine running the test script
    #    ssh-keygen -R $bastion_ssh_addr
    # is not needed and has not to be executed
    # as /root/.ssh/known_hosts does not exist at all in the worker context.
    # Not strictly needed in this context as each test
    # in openQA start from a clean environment

    my $cmd = join(' ',
        'ssh',
        '-E', SSH_LOG,
        SSH_VERBOSE,
        '-oStrictHostKeyChecking=accept-new', # always use accept-new is fine here as this cmd is executed on the worker that is supposed to have a recent ssh client
        ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip}),
        'whoami');
    assert_script_run($cmd);

    # one more without StrictHostKeyChecking=accept-new just to verify it is ok
    ipaddr2_ssh_bastion_assert_script_run(
        cmd => 'whoami',
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_internal_key_accept

    ipaddr2_internal_key_accept()

For the worker to accept the host key of the internal VMs.
This function always use cloudadmin as user in any ssh connections.

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<key_checking> - optional parameter allow to tune value for StrictHostKeyChecking
                        ssh option. default to 'accept-new'

=back
=cut

sub ipaddr2_internal_key_accept(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{key_checking} //= 'accept-new';

    my $key_policy = '-oStrictHostKeyChecking=' . $args{key_checking};
    my $bastion_ssh_addr = ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip});
    my ($vm_name, $vm_addr, $ret, $start_time, $exit_code, $score);
    foreach my $i (1 .. 2) {
        $vm_name = ipaddr2_get_internal_vm_private_ip(id => $i);
        $vm_addr = USER . "\@$vm_name";

        # The worker reaches the two remote internal VMs
        # through the bastion VM, using ssh proxy mode.
        # The connection between the worker and the internalVM
        # is used for test purpose, to observe from the external
        # what is going on inside the SUT.

        # Start by waiting that the ssh port is open
        $start_time = time();
        $exit_code = 1;
        $score = 0;
        while ((time() - $start_time) < 300) {
            $exit_code = ipaddr2_ssh_bastion_script_run(
                cmd => "nc -vz -w 1 $vm_name 22",
                bastion_ip => $args{bastion_ip});
            # sleep before to evaluate as, even if port is open,
            # it could take more time to be able to establish
            # the first ssh connection.
            sleep 10;

            # this score mechanism penalize more those systems
            # that are not ready when reaching this code.
            $score += (defined($exit_code) && $exit_code eq 0) ? +1 : -1;
            last if $score > 1;
        }
        die "ssh port 22 not available on VM $vm_name" if (!(defined($exit_code) && $exit_code eq 0));

        # Try two different variants of the same command.
        $ret = script_run(join(' ',
                'ssh',
                '-E', SSH_LOG,
                SSH_VERBOSE,
                $key_policy,
                '-oConnectionAttempts=120',
                '-J', $bastion_ssh_addr,
                $vm_addr,
                'whoami',
                '2>>' . SSH_PROXY_LOG));

        if ($ret) {
            $ret = script_run(join(' ',
                    'ssh',
                    '-E', SSH_LOG,
                    SSH_VERBOSE,
                    $vm_addr,
                    "-oProxyCommand=\"ssh $bastion_ssh_addr -oConnectionAttempts=120 -W %h:%p\"",
                    $key_policy,
                    #'-oConnectionAttempts=60',
                    'whoami'));
            die "2 StrictHostKeyChecking --> ret:$ret" if $ret;
        }
        # one more without StrictHostKeyChecking just to verify it is ok
        ipaddr2_ssh_internal(id => $i,
            cmd => 'whoami',
            bastion_ip => $args{bastion_ip});
    }
}

=head2 ipaddr2_internal_key_gen

    ipaddr2_internal_key_gen()

Create, on the /tmp folder of the Worker, two ssh key sets:
one ssh key pair for each internal VM.
Then upload in each internal VM the ssh key pair using scp
and add pub key in the authorized_keys of the other internal VM.
It also runs the first connection to accept the host keys.
All this process it to have a suitable key set to allow password-less ssh between the two internal VM:
it is later on needed by crm cluster init/join

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<key_checking> - optional parameter allow to tune value for StrictHostKeyChecking
                        ssh option. default to 'accept-new'

=item B<user> - set custom user name. Default is cloudadmin.
                User root activate special behavior. This argument is needed as crm needs keys for different users
                when operate in root or rootless mode.

=back
=cut

sub ipaddr2_internal_key_gen(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{key_checking} //= 'accept-new';
    $args{user} //= USER;

    my $key_policy = '-oStrictHostKeyChecking=' . $args{key_checking};
    my $bastion_ssh_addr = ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip});
    my $user_ssh = ($args{user} eq 'root') ? '/root/' : "/home/$args{user}/";
    $user_ssh .= '.ssh';
    my $ssh_own = ($args{user} eq 'root') ? 'root:root' : "$args{user}:users";
    my ($vm_name, $vm_addr, $this_tmp, $remote_key_tmp_path, $remote_key_home_path);
    my @pubkey;
    foreach my $i (1 .. 2) {
        $vm_name = ipaddr2_get_internal_vm_private_ip(id => $i);
        # always use cloudadmin for ssh connections even when $args{user} is root
        $vm_addr = USER . "\@$vm_name";

        # Assert if the folder ~/.ssh does not exist in the VM
        ipaddr2_ssh_internal(id => $i,
            cmd => "sudo [ -d $user_ssh ]",
            bastion_ip => $args{bastion_ip});

        # Generate public/private keys pair for cloudadmin user on the internal VMs.
        # Generate them on the openQA worker, in a folder within /tmp.
        # The keys is distributed using ssh and scp in Proxy mode.
        $this_tmp = ipaddr2_get_worker_tmp_for_internal_vm(id => $i);
        #assert_script_run("rm -rf $this_tmp");
        assert_script_run("mkdir -p $this_tmp");
        assert_script_run(join(' ',
                'ssh-keygen',
                '-N ""',
                '-t rsa',
                "-C \"Temp internal cluster key for $args{user} on $vm_name\"",
                '-f', "$this_tmp/" . SSH_KEY_ID));

        # Save the ssh public key for later
        push @pubkey, script_output("cat $this_tmp/" . SSH_KEY_ID . '.pub');

        foreach my $this_key (SSH_KEY_ID, SSH_KEY_ID . '.pub') {
            $remote_key_tmp_path = "/tmp/$args{user}";
            ipaddr2_ssh_internal(id => $i,
                cmd => "mkdir -p  $remote_key_tmp_path",
                bastion_ip => $args{bastion_ip});
            $remote_key_tmp_path .= "/$this_key";
            $remote_key_home_path = "$user_ssh/$this_key";
            assert_script_run(join(' ',
                    'scp',
                    '-J', $bastion_ssh_addr,
                    join('/', $this_tmp, $this_key),
                    "$vm_addr:$remote_key_tmp_path"));

            my @install_key_cmds = (
                "sudo mv $remote_key_tmp_path $remote_key_home_path",
                "sudo chown $ssh_own $remote_key_home_path",
                "sudo chmod 0600 $remote_key_home_path",
                "sudo ls -lai $remote_key_home_path");

            foreach (@install_key_cmds) {
                ipaddr2_ssh_internal(id => $i,
                    cmd => $_,
                    bastion_ip => $args{bastion_ip});
            }
        }
    }

    # take pub key from one internal VM and write it in the authorized_keys file
    # of other VM
    my %auth_args = (
        bastion_ip => $args{bastion_ip},
        user => $args{user},
        key_checking => $args{key_checking});
    $auth_args{src} = 1;
    $auth_args{dst} = 2;
    ipaddr2_internal_key_authorize(%auth_args);

    $auth_args{src} = 2;
    $auth_args{dst} = 1;
    ipaddr2_internal_key_authorize(%auth_args);
}

=head2 ipaddr2_internal_key_authorize

    ipaddr2_internal_key_authorize(
        src          => 1,
        dst          => 2,
        user         => 'cloudadmin',
        key_checking => 'accept-new');

Helper for C<ipaddr2_internal_key_gen> and contribute to
establish password-less SSH access from a source internal VM to a
destination internal VM for a specific user.
It takes the public SSH key of the source VM (previously generated and stored on the openQA worker)
and adds it to the C<authorized_keys> file on the destination VM.

This function must be called twice, once for each direction (e.g., VM1 to VM2,
and then VM2 to VM1).

The process involves:
1. Copying the source VM's public key to a temporary location on the destination VM via the bastion host.
2. Appending the key to the correct C<authorized_keys> file on the destination VM.
3. Performing a test SSH connection from the source to the destination to verify the setup.

=over

=item B<src> - id of the internal VM that want to have its public key recorded on the other VM

=item B<dst> - id of the internal VM has to add the other VM pub key in its authorized_key list

=item B<key_checking> - tune value for StrictHostKeyChecking ssh option.

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<user> - set custom user name. Default is cloudadmin.
                User root activate special behavior. This argument is needed as crm needs keys for different users
                when operate in root ro rootless mode.

=back
=cut

sub ipaddr2_internal_key_authorize(%args) {
    foreach (qw(src dst key_checking)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{user} //= USER;

    my $vm_name;
    my $remote_key_tmp_path = '/tmp/other_vm';
    my $this_key = SSH_KEY_ID . '.pub';
    my $authorize_file = ($args{user} eq 'root') ? '/root/' : "/home/$args{user}/";
    $authorize_file .= '.ssh/authorized_keys';
    my $key_policy = '-oStrictHostKeyChecking=' . $args{key_checking};

    # This is the destination VM where to register the public key
    $vm_name = ipaddr2_get_internal_vm_private_ip(id => $args{dst});

    # Create a place where to temporary upload the key
    ipaddr2_ssh_internal(id => $args{dst},
        cmd => "mkdir -p $remote_key_tmp_path",
        bastion_ip => $args{bastion_ip});
    # Upload the pub key to a temporary location in the dst VM
    assert_script_run(join(' ',
            'scp',
            '-J', ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip}),
            join('/',
                # This is the public key to be registered, so the source
                ipaddr2_get_worker_tmp_for_internal_vm(id => $args{src}),
                $this_key),
            USER . "\@$vm_name:$remote_key_tmp_path/$this_key"));

    # Append the public key to the authorized_keys file of the destination VM
    my $reg_cmd = ($args{user} eq 'root') ?
      "sudo sh -c \"cat $remote_key_tmp_path/$this_key >> $authorize_file\"" :
      "cat $remote_key_tmp_path/$this_key >> $authorize_file";
    ipaddr2_ssh_internal(id => $args{dst},
        cmd => $reg_cmd,
        bastion_ip => $args{bastion_ip});
    # first ssh connection: here it is one of the most complex ssh somersault ever ;-)
    # 1. create a ssh connection from the worker running the test to one of the two internal VMs,
    #    through the bastion. This is mostly done by ipaddr2_ssh_internal.
    # 2. once on one of the internal vm, run a command there. Command itself is a ssh connection
    #    to the other internal vm.
    # 3. once on the other internal VM, run a simple whoami.
    # This VM to VM password-less connection is needed by crm cluster join.
    # $args{user} can be both 'cloudadmin' or 'root' but ssh connection between  Worker and
    # the first internal vm is always done as cloudadmin (crm only care VM to VM ssh configuration).
    my $f_cmd = join(' ',
        'ssh',
        '-E', SSH_LOG,
        "$args{user}\@$vm_name",
        $key_policy,
        'whoami');
    $f_cmd = "sudo $f_cmd" if ($args{user} eq 'root');
    ipaddr2_ssh_internal(id => $args{src},
        cmd => $f_cmd,
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_deployment_sanity

    ipaddr2_deployment_sanity()

Run some checks on the existing deployment using the
az command line. Die in case of failure
=cut

sub ipaddr2_deployment_sanity {
    my $rg = ipaddr2_azure_resource_group();
    my $res = az_group_name_get();
    my $count = grep(/$rg/, @$res);
    die "There are not exactly one but $count resource groups with name $rg" unless $count eq 1;

    $res = az_vm_list(resource_group => $rg, query => '[].name');
    $count = grep(/$bastion_vm_name/, @$res);
    die "There are not exactly 3 VMs but " . ($#{$res} + 1) unless ($#{$res} + 1) eq 3;
    die "There are not exactly 1 but $count VMs with name $bastion_vm_name" unless $count eq 1;

    foreach (1 .. 2) {
        my $vm = ipaddr2_get_internal_vm_name(id => $_);
        $res = az_vm_instance_view_get(
            resource_group => $rg,
            name => $vm);
        # Expected return is
        # [ "PowerState/running", "VM running" ]
        $count = grep(/running/, @$res);
        # 2 is two occurrence of the word 'running' for one VM
        die "VM $vm is not fully running" unless $count eq 2;
    }
}

=head2 ipaddr2_os_sanity

    ipaddr2_os_sanity()

Run some OS level checks on all the VMs in the deployment.
die in case of failure. Tests are targeting all the VM.
Tests are independent by the cluster status.

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<user> - user expected to be able to ssh connect password-less from one internal VM to the other.
                Default is cloudadmin.

=back
=cut

sub ipaddr2_os_sanity(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{user} //= USER;

    ipaddr2_os_network_sanity(bastion_ip => $args{bastion_ip});
    ipaddr2_os_connectivity_sanity(bastion_ip => $args{bastion_ip});
    ipaddr2_os_ssh_sanity(user => $args{user}, bastion_ip => $args{bastion_ip});

    foreach (1 .. 2) {
        ipaddr2_ssh_internal(id => $_,
            cmd => 'sudo systemctl is-system-running',
            bastion_ip => $args{bastion_ip});
    }

    ipaddr2_cloudinit_sanity(bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_cluster_sanity

    ipaddr2_cluster_sanity()

Run some cluster level checks

=over

=item B<id> - ID of the internal VM where to run the crm commands. Default is 1.

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_cluster_sanity(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{id} //= 1;

    my $crm_status = ipaddr2_ssh_internal_output(id => $args{id},
        cmd => 'sudo crm status',
        bastion_ip => $args{bastion_ip});

    die "Issue in the cluster health" if cluster_status_matches_regex($crm_status);

    ipaddr2_ssh_internal(id => $args{id},
        cmd => "sudo $crm_mon_cmd",
        bastion_ip => $args{bastion_ip});

    my $crm_configure = ipaddr2_ssh_internal_output(id => $args{id},
        cmd => 'sudo crm configure show',
        bastion_ip => $args{bastion_ip});

    my @resources = $crm_configure =~ /primitive/g;
    die "Cluster on VM $args{id} has " . scalar @resources . " primitives instead of expected 3" unless (scalar @resources) eq 3;

    ipaddr2_ssh_internal(id => $args{id},
        cmd => '[ -f /usr/lib/ocf/resource.d/heartbeat/nginx ]',
        bastion_ip => $args{bastion_ip});
    ipaddr2_ssh_internal(id => $args{id},
        cmd => 'rpm -qf /usr/lib/ocf/resource.d/heartbeat/nginx',
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_os_connectivity_sanity

    ipaddr2_os_connectivity_sanity()

Run some OS level checks about internal connectivity.
die in case of failure

- bastion has to be able to ping the internal VM using the internal private IP
- bastion has to be able to ping the internal VM using the internal VM hostname

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_os_connectivity_sanity(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    # intentionally ignore the return as ping or nc
    # could be missing on the qcow2 running the test
    script_run(PING_CMD . " $args{bastion_ip}");
    script_run("nc -vz -w 1 $args{bastion_ip} 22");

    foreach my $i (1 .. 2) {
        # Check if the bastion is able to ping
        # the VM by hostname and private IP.
        # Check both using the hostname and the IP address.
        foreach my $addr (
            ipaddr2_get_internal_vm_private_ip(id => $i),
            ipaddr2_get_internal_vm_name(id => $i)) {
            # tracepath is not available by default in 12sp5
            # so only use ping and dig
            foreach my $cmd (PING_CMD, 'dig') {
                ipaddr2_ssh_bastion_assert_script_run(
                    cmd => "$cmd $addr",
                    bastion_ip => $args{bastion_ip});
            }
        }
    }

    # Check if the two internal VM can ping one to each other
    ipaddr2_ssh_internal(id => 1,
        cmd => join(' ', PING_CMD, ipaddr2_get_internal_vm_private_ip(id => 2)),
        bastion_ip => $args{bastion_ip});

    ipaddr2_ssh_internal(id => 2,
        cmd => join(' ', PING_CMD, ipaddr2_get_internal_vm_private_ip(id => 1)),
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_cloudinit_sanity

    ipaddr2_cloudinit_sanity()

Run some checks about cloud-init. These checks can be executed also
when a cloudinit script is not used to bootstrap the SUT.

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_cloudinit_sanity(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    foreach my $id (1 .. 2) {
        foreach (
            'grep -E "root|cloudadmin|hacluster" /etc/passwd',
            'sudo zypper se -s -i cloud-init',
            'cloud-init -v',
            'cloud-init status --wait --long',
            'sudo systemctl status cloud-init-local.service',
            'sudo systemctl status cloud-init.service',
            'sudo systemctl status cloud-config.service',
            'sudo systemctl status cloud-final.service') {
            ipaddr2_ssh_internal(id => $id,
                cmd => $_,
                timeout => 180,
                bastion_ip => $args{bastion_ip});
        }
    }
}

=head2 ipaddr2_logs_cloudinit

    ipaddr2_logs_cloudinit()

Print on the terminal some cloud-init related logs

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_logs_cloudinit(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    foreach my $id (1 .. 2) {
        foreach (
            'sudo cat /var/log/cloud-init.log',
            'sudo cat /var/log/cloud-init-output.log',
            'sudo cloud-init collect-logs',
            'sudo cloud-init analyze show',
            'sudo cloud-init analyze dump',
            'sudo cloud-init analyze blame',
            'sudo cloud-init analyze boot || echo "rc:$?"',
            'sudo cloud-init schema --system',
            'sudo dmesg -T | grep -i -e warning -e error -e fatal -e exception',
            'tail -n 3 /run/cloud-init/ds-identify.log'
        ) {
            ipaddr2_ssh_internal(id => $id,
                cmd => $_,
                bastion_ip => $args{bastion_ip});
        }
    }
}

=head2 ipaddr2_os_network_sanity

    ipaddr2_os_network_sanity()

Check that private IP are in the network configuration on the internal VMs

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_os_network_sanity(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    foreach (1 .. 2) {
        ipaddr2_ssh_internal(id => $_,
            cmd => "ip a show eth0 | grep -E \"inet .*$priv_ip_range\"",
            bastion_ip => $args{bastion_ip});
    }
}

=head2 ipaddr2_os_ssh_sanity

    ipaddr2_os_ssh_sanity()

Run some OS level checks on the various VMs ssh keys and configurations.
die in case of failure

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<user> - user supposed to be able to ssh connect password-less from one internal VM to the other.
                Value for this argument is used to decide the home folder where to look for the keys.
                Default is cloudadmin. User root activate some special logic.

=back
=cut

sub ipaddr2_os_ssh_sanity(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{user} //= USER;
    my $user_ssh = ($args{user} eq 'root') ? '/root/' : "/home/$args{user}/";
    $user_ssh .= '.ssh';

    foreach my $i (1 .. 2) {
        # Check if the folder ~/.ssh
        # exist in this internal VM
        ipaddr2_ssh_internal(id => $i,
            cmd => "sudo [ -d $user_ssh ]",
            bastion_ip => $args{bastion_ip});

        # Check if the private key ~.ssh/SSH_KEY_ID
        # exists in this internal VM.
        ipaddr2_ssh_internal(id => $i,
            cmd => "sudo [ -f $user_ssh/" . SSH_KEY_ID . ' ]',
            bastion_ip => $args{bastion_ip});

        # Use sudo in all commands from here, as ssh key owner could be root
        # Check authorized_keys content
        ipaddr2_ssh_internal(id => $i,
            cmd => "sudo cat $user_ssh/authorized_keys",
            bastion_ip => $args{bastion_ip});

        # Each internal VM has some pub keys from the pair
        # generated by the test code during the configure step
        ipaddr2_ssh_internal(id => $i,
            cmd => "sudo cat $user_ssh/authorized_keys | grep \"Temp internal cluster key for\"",
            bastion_ip => $args{bastion_ip});
    }

    # Check if ssh without password works between
    # the bastion and each of the internal VMs
    foreach my $i (1 .. 2) {
        ipaddr2_ssh_internal(id => $i,
            cmd => "whoami | grep " . USER,
            bastion_ip => $args{bastion_ip});

        # check root
        ipaddr2_ssh_internal(id => $i,
            cmd => 'sudo whoami | grep root',
            bastion_ip => $args{bastion_ip});
    }
}

=head2 ipaddr2_ssh_bastion_assert_script_run

    ipaddr2_ssh_bastion_assert_script_run(
        bastion_ip => '1.2.3.4',
        cmd => 'whoami');

Run a command on the bastion using assert_script_run

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<cmd> - command to run there

=back
=cut

sub ipaddr2_ssh_bastion_assert_script_run(%args) {
    croak("Argument < cmd > missing") unless $args{cmd};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    assert_script_run(join(' ',
            'ssh',
            '-E', SSH_LOG,
            ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip}),
            "'$args{cmd}'"));
}

=head2 ipaddr2_ssh_bastion_script_run

    my $ret = ipaddr2_ssh_bastion_script_run(
        bastion_ip => '1.2.3.4',
        cmd => 'whoami');

Run a command on the bastion using script_run

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<cmd> - command to run there

=back
=cut

sub ipaddr2_ssh_bastion_script_run(%args) {
    croak("Argument < cmd > missing") unless $args{cmd};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    return script_run(join(' ',
            'ssh',
            '-E', SSH_LOG,
            ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip}),
            "'$args{cmd}'"));
}

=head2 ipaddr2_ssh_bastion_script_output

    my $ret = ipaddr2_ssh_bastion_script_output(
        bastion_ip => '1.2.3.4',
        cmd => 'whoami');

Run a command on the bastion using script_output

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<cmd> - command to run there

=back
=cut

sub ipaddr2_ssh_bastion_script_output(%args) {
    croak("Argument < cmd > missing") unless $args{cmd};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    return script_output(join(' ',
            'ssh',
            '-E', SSH_LOG,
            ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip}),
            "'$args{cmd}'"));
}

=head2 ipaddr2_ssh_internal_cmd

    script_run(ipaddr2_ssh_internal_cmd(
        id => 2,
        bastion_ip => '1.2.3.4',
        cmd => 'whoami'));

Compose an ssh command. Command is composed to be executed on one of the two internal VM.
Command will use -J option to use the bastion as a proxy.
This function does not really execute any command, it only return a string.
Other functions can use result command string as input for various testapi functions,
like assert_script_run or script_output.

=over

=item B<id> - ID of the internal VM. Used to compose its name and as address for ssh.

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<cmd> - Command to be run on the internal VM.

=back
=cut

sub ipaddr2_ssh_internal_cmd(%args) {
    foreach (qw(id cmd)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    return join(' ',
        'ssh',
        '-E', SSH_LOG,
        '-J', ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip}),
        USER . '@' . ipaddr2_get_internal_vm_private_ip(id => $args{id}),
        "'$args{cmd}'",
        '2>>' . SSH_PROXY_LOG);
}

=head2 ipaddr2_ssh_internal

    ipaddr2_ssh_internal(id => 2,
        cmd => 'whoami',
        bastion_ip => '4.5.6.7',
        no_assert => 1);

Run a command on one of the two internal VM through the bastion
using the assert_script_run API

=over

=item B<id> - ID of the internal VM. Used to compose its name and as address for ssh.

=item B<cmd> - Command to be run on the internal VM.

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<timeout> - Execution timeout, default 90sec

=item B<no_assert> - If specified internally use 'script_run' in place of
                     'assert_script_run' and return the exit code.

=back
=cut

sub ipaddr2_ssh_internal(%args) {
    foreach (qw(id cmd)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{timeout} //= 90;

    my $command = ipaddr2_ssh_internal_cmd(
        id => $args{id},
        bastion_ip => $args{bastion_ip},
        cmd => $args{cmd});
    if ($args{no_assert}) {
        return script_run($command, timeout => $args{timeout});
    }
    else {
        assert_script_run($command, timeout => $args{timeout});
    }
}

=head2 ipaddr2_ssh_internal_output

    ipaddr2_ssh_internal_output(
        id => 2,
        bastion_ip => '1.2.3.4',
        cmd => 'whoami');

Runs $cmd  through the bastion on one of the two internal VMs using script_output.
Return the command output.

=over

=item B<id> - ID of the internal VM. Used to compose its name and as address for ssh.

=item B<cmd> - Command to be run on the internal VM.

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<timeout> - Execution timeout, default 90sec

=back
=cut

sub ipaddr2_ssh_internal_output(%args) {
    foreach (qw(id cmd)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{timeout} //= 90;

    return script_output(
        ipaddr2_ssh_internal_cmd(
            id => $args{id},
            bastion_ip => $args{bastion_ip},
            cmd => $args{cmd}),
        timeout => $args{timeout});
}

=head2 ipaddr2_cluster_create

    ipaddr2_cluster_create();

Initialize and configure the Pacemaker cluster on the two internal nodes

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<rootless> - Enable or disable the rootless mode. Default is normal root mode.

=back
=cut

sub ipaddr2_cluster_create(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{rootless} //= 0;

    ipaddr2_ssh_internal(id => 1,
        cmd => 'sudo crm cluster init -y --name DONALDUCK',
        bastion_ip => $args{bastion_ip});

    my $join_str = $args{rootless} ? USER . '@' : "";
    $join_str .= ipaddr2_get_internal_vm_private_ip(id => 1);
    ipaddr2_ssh_internal(id => 2,
        cmd => "sudo crm cluster join -y -c $join_str",
        bastion_ip => $args{bastion_ip});

    ipaddr2_ssh_internal(id => 1,
        cmd => 'sudo crm configure property maintenance-mode=true',
        bastion_ip => $args{bastion_ip});

    ipaddr2_ssh_internal(id => 1,
        cmd => join(' ',
            'sudo crm configure primitive',
            'rsc_ip_00',
            'ocf:heartbeat:IPaddr2',
            'meta target-role="Started"',
            'operations \$id="rsc_ip_RES-operations"',
            'op monitor interval="10s" timeout="20s"',
            "params ip=\"$frontend_ip\""),
        bastion_ip => $args{bastion_ip});

    ipaddr2_ssh_internal(id => 1,
        cmd => join(' ',
            'sudo crm configure primitive',
            WEB_RSC,
            'ocf:heartbeat:nginx',
            'configfile=/etc/nginx/nginx.conf',
            'op start timeout="40s" interval="0"',
            'op stop timeout="60s" interval="0"',
            'op monitor interval="10s" timeout="60s"',
            'meta migration-threshold="10"'),
        bastion_ip => $args{bastion_ip});

    ipaddr2_ssh_internal(id => 1,
        cmd => join(' ',
            'sudo crm configure primitive',
            'rsc_alb_00',
            'azure-lb',
            'port=62500',
            'op monitor  interval="10s" timeout="20s"'),
        bastion_ip => $args{bastion_ip});

    ipaddr2_ssh_internal(id => 1,
        cmd => join(' ',
            'sudo crm configure group',
            'rsc_grp_00', 'rsc_alb_00', 'rsc_ip_00', WEB_RSC),
        bastion_ip => $args{bastion_ip});

    ipaddr2_ssh_internal(id => 1,
        cmd => 'sudo crm configure property maintenance-mode=false',
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_cluster_check_version

    ipaddr2_cluster_check_version();

Check the version priv_ip_range of some cluster related packages like crmsh
For the moment all the commands are only executed on VM1.

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_cluster_check_version(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    foreach (
        'rpm -qf $(sudo which crm)',
        'sudo crm --version',
        'sudo zypper se -s -i crmsh') {
        ipaddr2_ssh_internal(id => 1,
            cmd => $_,
            bastion_ip => $args{bastion_ip});
    }
}

=head2 ipaddr2_scc_check

    my $is_registered = ipaddr2_scc_check(id => 1);

Check if image is registered by calling SUSEConnect -s.
Return 1 if all modules are registered, 0 if at least one is not.

=over

=item B<id> - VM id where to install and configure the web server

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_scc_check(%args) {
    croak("Argument < id > missing") unless $args{id};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    # Initially suppose is registered
    my $registered = 1;
    my $json = decode_json(ipaddr2_ssh_internal_output(
            id => $args{id},
            cmd => 'sudo SUSEConnect -s',
            bastion_ip => $args{bastion_ip}));
    foreach (@$json) {
        if ($_->{status} =~ '^Not Registered') {
            $registered = 0;
            last;
        }
    }
    return $registered;
}

=head2 ipaddr2_scc_register

    ipaddr2_scc_register(id => 1, scc_code => '1234567890');

Register the image. (For the moment) it only support registercloudguest endpoint.
Notice that this library also support registration through
ipaddr2_infra_deploy by adding couple of lines to cloud-init configuration file.

=over

=item B<id> - VM id where to install and configure the web server

=item B<scc_code> - registration code

=item B<scc_endpoint> - by default it is registercloudguest

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_scc_register(%args) {
    foreach (qw(id scc_code)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{scc_endpoint} //= 'registercloudguest';
    croak("SCC endpoint $args{scc_endpoint} is not supported.") unless ($args{scc_endpoint} eq 'SUSEConnect' || $args{scc_endpoint} eq 'registercloudguest');

    ipaddr2_ssh_internal(id => $args{id},
        cmd => "sudo $args{scc_endpoint} --clean",
        bastion_ip => $args{bastion_ip});

    my $forcenew = ($args{scc_endpoint} eq 'registercloudguest') ? '--force-new' : '';
    ipaddr2_ssh_internal(id => $args{id},
        cmd => "sudo $args{scc_endpoint} $forcenew -r \"$args{scc_code}\"",
        timeout => 360,
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_configure_web_server

    ipaddr2_configure_web_server(id => 1);

This function is in charge to:
    1. install the nginx package
    2. create a web page file
    3. enable and start the system

=over

=item B<id> - VM id where to install and configure the web server

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<nginx_root> - Optional argument: default is B</srv/www/htdocs>. This argument allows
                      to overwrite the location where the index.html, generated by the test and used to identify each node,
                      is stored. As the test is not using any nginx configuration file, this location has to match to the
                      default root folder configured by the particular version of nginx installed in the SUT.
                      Default value is/was value for nginx installable in 15sp5 and sp6.
                      12sp5 needs /usr/share/nginx/html

=item B<external_repo> - Optional argument: allow to add a PackageHub product

=back
=cut

sub ipaddr2_configure_web_server(%args) {
    croak("Argument < id > missing") unless $args{id};
    $args{nginx_root} //= '/srv/www/htdocs';

    my @nginx_cmds;
    # This is an optional set of commands
    # only enabled when external_repo is provided.
    # It is needed as nginx not available in 12sp5 repos
    push @nginx_cmds,
      "sudo SUSEConnect --product PackageHub/$args{external_repo}/x86_64" if ($args{external_repo});

    # Common commands
    push @nginx_cmds,
      'sudo zypper ref',
      'sudo zypper install -y nginx socat',    # socat is needed but not installed by default in 12sp5
      'echo "I am $(hostname)" > /tmp/index.html',
      "sudo mv /tmp/index.html $args{nginx_root}/index.html";

    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    ipaddr2_ssh_internal(id => $args{id},
        cmd => $_,
        timeout => 600,
        bastion_ip => $args{bastion_ip}) for (@nginx_cmds);
}

=head2 ipaddr2_repo_refresh

    ipaddr2_repo_refresh(id => 1);

Call zypper refresh

=over

=item B<id> - VM id where to install and configure the web server

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_repo_refresh(%args) {
    croak("Argument < id > missing") unless $args{id};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    ipaddr2_ssh_internal(id => $args{id},
        cmd => 'sudo zypper ref',
        timeout => 600,
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_repo_list

    ipaddr2_repo_list(id => 1);

List all configured zypper repos

=over

=item B<id> - VM id where to install and configure the web server

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_repo_list(%args) {
    croak("Argument < id > missing") unless $args{id};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    # record repo lr
    ipaddr2_ssh_internal(id => $args{id},
        cmd => "sudo zypper lr",
        bastion_ip => $args{bastion_ip});
    # record repo ls
    ipaddr2_ssh_internal(id => $args{id},
        cmd => "sudo zypper ls",
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_deployment_logs

    ipaddr2_deployment_logs()

Collect logs from the cloud infrastructure
=cut

sub ipaddr2_deployment_logs {
    my @diagnostic_log_files = az_vm_diagnostic_log_get(
        resource_group => ipaddr2_azure_resource_group());
    while (my $file = pop @diagnostic_log_files) {
        upload_logs($file, failok => 1);
    }
}

=head2 ipaddr2_infra_destroy

    ipaddr2_infra_destroy();

Destroy the deployment by deleting the resource group
=cut

sub ipaddr2_infra_destroy {
    az_group_delete(name => ipaddr2_azure_resource_group(), timeout => 600);
}

=head2 ipaddr2_get_internal_vm_name

    my $vm_name = ipaddr2_get_internal_vm_name(id => 2);

Compose and return a string for the vm name

=over

=item B<id> - VM id number

=back
=cut

sub ipaddr2_get_internal_vm_name(%args) {
    croak("Argument < id > missing") unless $args{id};
    return DEPLOY_PREFIX . "-vm-0$args{id}";
}

=head2 ipaddr2_get_internal_vm_private_ip

    my $private_ip = ipaddr2_get_internal_vm_private_ip(id => 2);

compose and return a string representing the VM private IP

=over

=item B<id> - VM id number

=back
=cut

sub ipaddr2_get_internal_vm_private_ip(%args) {
    croak("Argument < id > missing") unless $args{id};
    return $priv_ip_range . '.4' . $args{id};
}

=head2 ipaddr2_get_worker_tmp_for_internal_vm

    my $vm_tmp = ipaddr2_get_worker_tmp_for_internal_vm(2);

Return a path in /tmp of the worker used to store files associated
two one of the internal VM
=cut

sub ipaddr2_get_worker_tmp_for_internal_vm(%args) {
    croak("Argument < id > missing") unless $args{id};
    return "/tmp/" . ipaddr2_get_internal_vm_name(id => $args{id});
}

=head2 ipaddr2_crm_move

    ipaddr2_crm_move(destination => 2);

Move the rsc_web_00 resource to the indicated node

=over

=item B<destination> - VM id where to move the rsc_web_00 resource

=item B<id> - VM id where to run the command, not so important as long as it is in the cluster. Default 1.

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_crm_move(%args) {
    croak("Argument < destination > missing") unless $args{destination};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{id} //= 1;

    my $cmd = join(' ',
        'sudo crm resource move',
        WEB_RSC,
        ipaddr2_get_internal_vm_name(id => $args{destination}));
    ipaddr2_ssh_internal(id => $args{id},
        cmd => $cmd,
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_crm_clear

    ipaddr2_crm_clear();

Clear all location constrain used during the test

=over

=item B<id> - VM id where to run the command, not so important as long as it is in the cluster. Default 1.

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_crm_clear(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{id} //= 1;

    my $cmd = join(' ',
        'sudo crm resource clear',
        WEB_RSC);
    ipaddr2_ssh_internal(id => $args{id},
        cmd => $cmd,
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_wait_for_takeover

    die "Takeover does not happens in time" unless  ipaddr2_wait_for_takeover(destination => 2);

Wait that web server is responding from the node indicated by id.
This check is implemented running a curl request from the bastion
and using the virtual IP address as URL.
This is possible because the webserver on each node is configured 
to return its hostname in the response.
Return 1 as soon as it gets the id in the response. Return 0 if not within 10 minutes.

=over

=item B<destination> - VM id that from where the web server response is expected to come from

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_wait_for_takeover(%args) {
    croak("Argument < destination > missing") unless $args{destination};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    my $counter = 0;
    my $dest_vm = ipaddr2_get_internal_vm_name(id => $args{destination});

    while ($counter < 60) {
        if (ipaddr2_get_web(
                bastion_ip => $args{bastion_ip},
                web_url => $frontend_ip,
                str_match => $dest_vm)) {
            record_info("TAKE_OVER", "Webserver now reply from $dest_vm");
            return 1;
        }
        sleep 10;
        $counter++;
    }
    return 0;
}

=head2 ipaddr2_get_web

    ipaddr2_get_web(str_match => 'openqa_vm_01');

Runs a curl request from the bastion and using the virtual IP address as URL.
Return result of searching str_match in the curl response

=over

=item B<web_url> - webserver url

=item B<str_match> - string to search in the curl output

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_get_web(%args) {
    foreach (qw(web_url str_match)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    my $curl_ret = ipaddr2_ssh_bastion_script_output(
        bastion_ip => $args{bastion_ip},
        cmd => "curl -s http://$args{web_url}");

    return ($curl_ret =~ m/$args{str_match}/);
}

=head2 ipaddr2_test_master_vm

    ipaddr2_test_master_vm(id => 2);

Check the status on the VM that is supposed to have
the resources.

=over

=item B<id> - VM id that is expected to be master

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_test_master_vm(%args) {
    croak("Argument < id > missing") unless $args{id};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    # checks on the cluster side
    ipaddr2_ssh_internal(id => $args{id},
        cmd => 'sudo crm status',
        bastion_ip => $args{bastion_ip});

    my $vm = ipaddr2_get_internal_vm_name(id => $args{id});
    my $res;
    foreach my $resource (qw(rsc_web_00 rsc_alb_00 rsc_ip_00)) {
        $res = ipaddr2_ssh_internal_output(
            id => $args{id},
            cmd => "sudo crm resource failcount $resource show $vm",
            bastion_ip => $args{bastion_ip});
        die "Fail count is not 0 for resource $resource in $vm" unless ($res =~ m/value=0/);

        $res = ipaddr2_ssh_internal_output(
            id => $args{id},
            cmd => "sudo crm resource locate $resource",
            bastion_ip => $args{bastion_ip});
        die "Resource $resource is not running on $vm" unless ($res =~ m/is running on: $vm/);
    }

    my $crm_configure = ipaddr2_ssh_internal_output(id => $args{id},
        cmd => 'sudo crm configure show',
        bastion_ip => $args{bastion_ip});

    my @preferred = $crm_configure =~ /cli-prefer-.*/g;
    die "Cluster has " . scalar @preferred . " resources with preferred location instead of expected 1"
      unless (scalar @preferred) eq 1;
    #ssh_node1 'sudo crm configure show' | grep -E "cli-prefer-.*${MYNAME}-vm-02" || test_die "Cluster should now have one cli-prefer- with ${MYNAME}-vm-02"

    # test that the web page is reachable from the bastion
    # using the Azure LB front end IP
    die "The web server is not running on $vm" unless ipaddr2_get_web(
        bastion_ip => $args{bastion_ip},
        web_url => $frontend_ip,
        str_match => $vm);
    # test that the web page is reachable from the bastion
    # using the VM hostname where the web server is supposed to run
    die "The web server is not running on $vm" unless ipaddr2_get_web(
        bastion_ip => $args{bastion_ip},
        web_url => $vm,
        str_match => $vm);

    my $ps_ret = ipaddr2_ssh_internal_output(
        id => $args{id},
        cmd => 'ps -xa',
        bastion_ip => $args{bastion_ip});

    die "Nginx process not running on $vm" unless ($ps_ret =~ m/nginx/);

    # check IP
    $res = ipaddr2_ssh_internal_output(
        id => $args{id},
        cmd => 'ip a show eth0',
        bastion_ip => $args{bastion_ip});
    die "VirtualIP $frontend_ip should be on $vm" unless ($res =~ m/$frontend_ip/);

    # Check if the master internal VM can ping the virtual IP
    ipaddr2_ssh_internal(id => $args{id},
        cmd => join(' ', PING_CMD, $frontend_ip),
        bastion_ip => $args{bastion_ip});
}

=head2 ipaddr2_test_other_vm

    ipaddr2_test_other_vm(id => 1);

Check the status on the VM that is supposed not to have
the resources.

=over

=item B<id> - VM id that is expected not to be master

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_test_other_vm(%args) {
    croak("Argument < id > missing") unless $args{id};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    # checks on the cluster side
    ipaddr2_ssh_internal(id => $args{id},
        cmd => 'sudo crm status',
        bastion_ip => $args{bastion_ip});

    my $vm = ipaddr2_get_internal_vm_name(id => $args{id});
    my $res;
    foreach my $resource (qw(rsc_web_00 rsc_alb_00 rsc_ip_00)) {
        $res = ipaddr2_ssh_internal_output(
            id => $args{id},
            cmd => "sudo crm resource locate $resource",
            bastion_ip => $args{bastion_ip});
        die "Resource $resource is running on $vm and should not" if ($res =~ m/is running on: $vm/);
    }
}

=head2 ipaddr2_network_peering_create

    ipaddr2_network_peering_create(ibsm_rg => 'IBSmMyRg');

Create network peering

=over

=item B<ibsm_rg> - Resource group of the IBSm

=back
=cut

sub ipaddr2_network_peering_create(%args) {
    croak 'Missing mandatory argument < ibsm_rg >' unless $args{ibsm_rg};

    ibsm_network_peering_azure_create(
        ibsm_rg => $args{ibsm_rg},
        sut_rg => ipaddr2_azure_resource_group(),
        name_prefix => DEPLOY_PREFIX);
}

=head2 ipaddr2_repos_add_server_to_hosts

    ipaddr2_repos_add_server_to_hosts(
        ibsm_ip => get_required_var('IBSM_IP'),
        incident_repos => 'AAAA,BBBB');

Add download.suse.de server to hosts by specifying IBSM address

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=item B<ibsm_ip> - IP of the IBSm

=item B<incident_repos> - Comma separated list of incident repos

=item B<repo_host> - host name of the repo server. Default is download.suse.de.

=back
=cut

sub ipaddr2_repos_add_server_to_hosts(%args) {
    foreach (qw(ibsm_ip incident_repos)) {
        croak("Argument < $_ > missing") unless $args{$_}; }
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    $args{repo_host} //= 'download.suse.de';

    foreach my $id (1 .. 2) {
        ipaddr2_ssh_internal(id => $id,
            cmd => "echo \"$args{'ibsm_ip'} $args{repo_host}\" | sudo tee -a /etc/hosts",
            bastion_ip => $args{bastion_ip});
    }

    # Add repos
    my $count = 0;
    my @repos = split(/,/, $args{incident_repos});
    while (defined(my $maintrepo = shift @repos)) {
        next if $maintrepo =~ /^\s*$/;
        if ($maintrepo =~ /Development-Tools/ or $maintrepo =~ /Desktop-Applications/) {
            record_info("MISSING REPOS", "There are repos in this incident, that are not uploaded to IBSM. ($maintrepo). Later errors, if they occur, may be due to these.");
            next;
        }
        my $zypper_cmd = "sudo zypper --no-gpg-checks ar -f -n TEST_$count $maintrepo TEST_$count";

        foreach my $id (1 .. 2) {
            ipaddr2_ssh_internal(id => $id,
                cmd => $zypper_cmd,
                bastion_ip => $args{bastion_ip});
        }
        $count++;
    }
}

=head2 ipaddr2_patch_system

    ipaddr2_patch_system();

Patch system

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_patch_system(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    my @vms = ();
    foreach my $id (1 .. 2) {
        my $vm_private_ip = ipaddr2_get_internal_vm_private_ip(id => $id);
        push @vms, $vm_private_ip;

        ipaddr2_ssh_internal(id => $id,
            cmd => "sudo zypper -n ref",
            timeout => 1500,
            bastion_ip => $args{bastion_ip});
    }

    # zypper patch
    my $host_ip = ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip});
    foreach my $vm_ip (@vms) {
        ssh_fully_patch_system("-J $host_ip cloudadmin@" . "$vm_ip");
    }

    foreach my $vm_id (1 .. 2) {
        # To avoid the zypper lock issue
        ipaddr2_ssh_internal(id => $vm_id,
            cmd => 'sudo systemctl mask packagekit; sudo systemctl stop packagekit; while pgrep packagekitd; do sleep 1; done',
            timeout => 120,
            no_assert => 1,
            bastion_ip => $args{bastion_ip});

        # on 12-SP5, reboot causes the VM to restart too fast which can cause failures with
        # assert_script_run. Use script_run instead to avoid that
        ipaddr2_ssh_internal(id => $vm_id,
            cmd => 'sudo reboot',
            timeout => 60,
            no_assert => 1,
            bastion_ip => $args{bastion_ip});
    }

    # check if the VMs have rebooted successfully
    foreach my $ip (@vms) {
        my $timeout = 600;
        while ($timeout > 0) {
            if (script_run("ssh $host_ip 'nc -vz -w 1 $ip 22'") != 0) {
                record_info("waiting $ip boot");
                sleep 30;
                $timeout = $timeout - 30;
            }
            else {
                record_info("$ip reboot successfully");
                last;
            }
        }
        die "$ip failed to reboot" if ($timeout <= 0);
    }

    # wait quit zypper
    foreach my $v_id (1 .. 2) {
        my $loop = 20;
        while ($loop > 0) {
            my $ret = ipaddr2_ssh_internal(
                id => $v_id,
                cmd => 'pgrep "zypper|purge-kernels|rpm"',
                timeout => 60,
                no_assert => 1,
                bastion_ip => $args{bastion_ip});
            if ($ret == 0) {
                record_info('zypper process running, need to quit');
                sleep 30;
                $loop = $loop - 1;
            }
            else {
                record_info('There is no zypper progress');
                last;
            }
        }
        die "On machine $v_id, there are still zypper progress which will cause zypper se fail" if ($loop <= 0);
    }
}

=head2 ipaddr2_scc_addons

    ipaddr2_scc_addons(scc_addons => get_var('SCC_ADDONS'));

Register addons on SUT

=over

=item B<scc_addons> - List of scc addons as usually provided by SCC_ADDONS variable

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_scc_addons(%args) {
    croak 'Missing mandatory argument < scc_addons >' unless $args{scc_addons};
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    my $host_ip = ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip});
    my @addons = split(/,/, $args{scc_addons});

    foreach my $id (1 .. 2) {
        # Register through an external library function register_addon.
        # Compose the command to run the addons registration on the two internal VMs.
        my $remote_cmd = join(' ', '-J', $host_ip,
            USER . '@' . ipaddr2_get_internal_vm_private_ip(id => $id));
        for my $addon (@addons) {
            next if ($addon =~ /^\s+$/);
            register_addon($remote_cmd, $addon);
        }
    }
}

=head2 ipaddr2_cleanup

  ipaddr2_cleanup();

Collect logs, remove the IBSm network peering if present and destroy the deployment.

=over

=item B<diagnostic> - Optionally collect diagnostic logs, from setting IPADDR2_DIAGNOSTIC

=item B<cloudinit> - Optionally collect cloudinit logs, from setting IPADDR2_CLOUDINIT

=item B<ibsm_rg> - Optionally delete the network peering to IBSs, from setting IBSM_RG

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_cleanup(%args) {
    $args{diagnostic} //= 0;
    $args{cloudinit} //= 1;

    ipaddr2_deployment_logs() if ($args{diagnostic} == 1);
    ipaddr2_logs_cloudinit() unless ($args{cloudinit} == 0);
    if ($args{ibsm_rg}) {
        ibsm_network_peering_azure_delete(
            sut_rg => ipaddr2_azure_resource_group(),
            sut_vnet => get_current_job_id(),
            ibsm_rg => $args{ibsm_rg});
    }
    ipaddr2_infra_destroy();
}

=head2 ipaddr2_logs_collect_cmds

    my @cmds = ipaddr2_logs_collect_cmds();

Returns a list of commands to collect logs from the ipaddr2 cluster.
=cut

sub ipaddr2_logs_collect_cmds {
    my @log_list = (
        {
            name => 'cloudregister',
            remote_log => 1,
            f_log => sub {
                my $id = shift;
                return {
                    file => '/var/log/cloudregister'}; }
        },
        {
            name => 'crm_report',
            remote_log => 1,
            f_log => sub {
                my $id = shift;
                my $file = "/var/log/crm_report_$id";
                return {
                    cmd => "sudo crm report $file",
                    file => $file . '.tar.gz'}; }
        },
        {
            name => 'y2logs',
            remote_log => 1,
            f_log => sub {
                my $id = shift;
                my $file = "/tmp/y2logs_$id.tar.gz";
                return {
                    cmd => "sudo save_y2logs $file",
                    file => $file}; }
        },
        {
            name => 'supportconfig',
            remote_log => 1,
            timeout => 600,
            f_log => sub {
                my $id = shift;
                my $file = "supportconfig_$id";
                return {
                    cmd => "sudo supportconfig -R /var/tmp -B $file -x AUDIT && sudo chmod 0755 /var/tmp/scc_$file.txz",
                    file => "/var/tmp/scc_$file.txz"}; }
        },
        {f_log => sub { return {file => SSH_LOG}; }},
        {f_log => sub { return {file => SSH_PROXY_LOG}; }}
    );
    return @log_list;
}

=head2 ipaddr2_logs_collect

    ipaddr2_logs_collect();

Collect logs from the ipaddr2 cluster.

=over

=item B<bastion_ip> - Public IP address of the bastion. Calculated if not provided.
                      Providing it as an argument is recommended
                      to avoid having to query Azure to get it.

=back
=cut

sub ipaddr2_logs_collect(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();
    my $bastion_ssh_addr = ipaddr2_bastion_ssh_addr(bastion_ip => $args{bastion_ip});
    my $vm_addr;
    my $worker_tmp_dir;
    my $scp_ret;
    my $local_file;
    my %log_data;
    my $remote_file;
    my $timeout;

    # Iterate over all the logs
    foreach my $log (ipaddr2_logs_collect_cmds()) {
        $scp_ret = 0;

        if (defined $log->{remote_log} && $log->{remote_log} == 1) {
            $timeout = $log->{timeout} // 300;
            # Iterate through each remote VM to generate and collect logs.
            foreach my $id (1 .. 2) {
                $vm_addr = USER . '@' . ipaddr2_get_internal_vm_private_ip(id => $id);
                $worker_tmp_dir = ipaddr2_get_worker_tmp_for_internal_vm(id => $id);
                assert_script_run("mkdir -p $worker_tmp_dir || echo 'Folder $worker_tmp_dir already exist'");
                %log_data = %{$log->{f_log}->($id)};
                $remote_file = $log_data{file};

                # Execute command on the remote VM to generate the log file, if there is a command to run.
                ipaddr2_ssh_internal(
                    id => $id,
                    bastion_ip => $args{bastion_ip},
                    no_assert => 1,
                    timeout => $timeout,
                    cmd => $log_data{cmd}) if (defined $log_data{cmd});

                # Download the generated file from the remote VM to the local worker.
                my ($filename) = $remote_file =~ m|/([^/]+)$|;
                $filename //= $remote_file;    # Fallback
                $local_file = "$worker_tmp_dir/$filename";
                record_info("bastion_ssh_addr:$bastion_ssh_addr vm_addr:$vm_addr ", join(' ',
                        'scp',
                        '-J', $bastion_ssh_addr,
                        "$vm_addr:$remote_file", $local_file));

                $scp_ret = script_run(join(' ',
                        'scp',
                        '-J', $bastion_ssh_addr,
                        "$vm_addr:$remote_file", $local_file));

                # If download was successful or file is local, upload the local file to openQA.
                upload_logs($local_file) if ($scp_ret == 0);
            }
        } else {
            # call it without id
            %log_data = %{$log->{f_log}->()};
            upload_logs($log_data{file});
        }
    }
}

=head2 ipaddr2_ssh_intrusion_detection

    my $ret = ipaddr2_ssh_intrusion_detection();

  Analyze sshd logs for failed login attempts using journalctl.
  Verifies that sshd logs do not contain login attempts from unexpected public IP addresses.
  Report any external IP is found.
  return 0 if no problem detected.

=over

=item B<bastion_ip> - Public IP of the bastion host.

=back
=cut

sub ipaddr2_ssh_intrusion_detection(%args) {
    $args{bastion_ip} //= ipaddr2_bastion_pubip();

    my $attempts;
    my $total_attempts = 0;
    my %users;
    my %ips;
    my %report;
    my @suspicious_ips;
    my %allowed_ips = map { $_ => 1 } (
        $args{bastion_ip},
        ipaddr2_get_internal_vm_private_ip(id => 1),
        ipaddr2_get_internal_vm_private_ip(id => 2),
    );
    my $cmd = 'sudo journalctl -u sshd | grep "Connection closed by" || true';

    my @log_outputs;

    push @log_outputs, {
        bastion => ipaddr2_ssh_bastion_script_output(bastion_ip => $args{bastion_ip}, cmd => $cmd)};

    foreach my $id (1 .. 2) {
        push @log_outputs, {
            ipaddr2_get_internal_vm_name(id => $id) =>
              ipaddr2_ssh_internal_output(id => $id,
                bastion_ip => $args{bastion_ip}, cmd => $cmd)};
    }

    foreach my $log_hash_ref (@log_outputs) {
        while (my ($vm_name, $log_output) = each %$log_hash_ref) {
            $attempts = 0;
            %users = ();
            %ips = ();

            foreach my $line (split /\n/, $log_output) {
                # Regex to capture user and IP for both 'authenticating user' and 'invalid user'
                if ($line =~ /Connection closed by (?:authenticating|invalid) user (\S+) (\S+)/) {
                    my ($user, $ip) = ($1, $2);
                    $users{$user}++;
                    $ips{$ip}++;
                    $attempts++;
                }
            }

            $report{$vm_name}{attempts} = $attempts;
            $report{$vm_name}{users} = [keys %users];
            $report{$vm_name}{ips} = [keys %ips];

            next if $attempts == 0;
            record_info("SSHD Log Analysis for $vm_name",
                "Found $report{$vm_name}{attempts} login attempts. Users: @{$report{$vm_name}{users}}. IPs: @{$report{$vm_name}{ips}}");
            $total_attempts += $attempts;
            foreach my $ip (@{$report{$vm_name}{ips}}) {
                my $ip_obj = NetAddr::IP::Lite->new($ip);
                # Flag any IP that is not in the allowed list and is not a private address.
                #if ($ip_obj && !$ip_obj->is_private() && !$allowed_ips{$ip}) {
                if ($ip_obj && !$allowed_ips{$ip}) {
                    push @suspicious_ips, $ip;
                }
            }
        }
    }
    return 0 unless $total_attempts > 0;

    if (@suspicious_ips) {
        record_info(
            "INTRUSION ATTEMPT DETECTED:" .
              "Unexpected external login attempts found from IPs: " . join(', ', @suspicious_ips));
    }
    return $total_attempts;
}


1;
