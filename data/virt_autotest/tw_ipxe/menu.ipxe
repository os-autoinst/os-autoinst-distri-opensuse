# This is the ipxe menu for installing TW on O3
# path: user@ariel:/srv/tftpboot/ipxe/menu.ipxe
# ---------- file begin ----------
#!ipxe

# dhcp should be already done by boot script
#dhcp

#set variables
set menu-timeout 30000
set server-name netboot
iseq ${next-server} 10.150.1.11   && set server-name openqa.opensuse.org ||
iseq ${next-server} 10.168.192.10 && set server-name qe.nue2.suse.org ||
iseq ${next-server} 10.162.0.1    && set server-name qanet.qa.suse.de ||

:start
menu iPXE boot menu for ${server-name}

item --gap --             -- OS Inst (You will be prompted to edit cmdline after selecting an os) --
item --gap --             -------------------------------- x86_64 ----------------------------------
item --key l leapS1       (L)Boot openSUSE Leap15.5 from download.opensuse.org / ttyS1 (http)
item --key e leapS2       (E)Boot openSUSE Leap15.5 from download.opensuse.org / ttyS2 (http)
item --key a leapS1auto   (A)Boot openSUSE Leap15.5 / ttyS1 autoyast=https://is.gd/oqaay4
item --key k tumbleweed_KVM_autoyast (K)Boot openSUSE Tumbleweed KVM server from O3 repo with autoyast/ ttyS1 (http)
item --key z tumbleweed_XEN_autoyast (Z)Boot openSUSE Tumbleweed XEN server from O3 repo with autoyast/ ttyS1 (http)
item --key t tumbleweedS1 (T)Boot openSUSE Tumbleweed from download.opensuse.org / ttyS1 (http)
item --key s tumbleweedS2 (S)Boot openSUSE Tumbleweed from download.opensuse.org / ttyS2 (http)
item --key M memtest64efi (M)Memtest 64 EFI
item --gap --             -------------------------------- AArch64 ---------------------------------
item --key o leapS1aarch64auto (O)Boot openSUSE Leap15.5 / ttyS1 autoyast=https://is.gd/oqaay4
item --gap --             ------------------------- Advanced options -------------------------------
item --key N nbxyz        (N)Netboot.xyz (x86_64 / aarch64)
item --key c config       Configure settings
item --key i shell        (i)Drop to iPXE shell
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue BIOS boot 

choose --timeout ${menu-timeout} --default exit selected || goto cancel
set menu-timeout 0
goto ${selected}

:leapS1
set kernel http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/x86_64/loader/linux
set cmdline usessh=1 sshpassword=linux network=1 install=http://download.opensuse.org/distribution/leap/15.5/repo/oss/ console=ttyS1,115200n8 root=/dev/ram0 initrd=initrd textmode=1
set initrd http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/x86_64/loader/initrd
goto editandboot

:leapS2
set kernel http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/x86_64/loader/linux
set cmdline usessh=1 sshpassword=linux network=1 install=http://download.opensuse.org/distribution/leap/15.5/repo/oss/ console=ttyS2,115200n8 root=/dev/ram0 initrd=initrd textmode=1
set initrd http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/x86_64/loader/initrd
goto editandboot

:leapS1auto
set kernel http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/x86_64/loader/linux
set cmdline network=1 install=http://download.opensuse.org/distribution/leap/15.5/repo/oss/ root=/dev/ram0 initrd=initrd textmode=1 console=tty console=ttyS1,115200 autoyast=https://is.gd/oqaay4 rootpassword=opensuse
set initrd http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/x86_64/loader/initrd
goto editandboot

:tumbleweed_KVM_autoyast
set kernel http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-x86_64-CURRENT/boot/x86_64/loader/linux
set cmdline install=http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-x86_64-CURRENT textmode=1 plymouth.enable=0 Y2DEBUG=1 console=ttyS1,115200 linuxrc.log=/dev/ttyS1 linuxrc.core=/dev/ttyS1 linuxrc.debug=4,trace reboot_timeout=0  kernel.softlockup_panic=1 video=1024x768 vt.color=0x07 quiet autoyast=http://openqa.opensuse.org/assets/other/autoyast_opensuse_kvm_two_disk.xml
set initrd http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-x86_64-CURRENT/boot/x86_64/loader/initrd
goto editandboot

:tumbleweed_XEN_autoyast
set kernel http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-x86_64-CURRENT/boot/x86_64/loader/linux
set cmdline install=http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-x86_64-CURRENT textmode=1 plymouth.enable=0 Y2DEBUG=1 console=ttyS1,115200 linuxrc.log=/dev/ttyS1 linuxrc.core=/dev/ttyS1 linuxrc.debug=4,trace reboot_timeout=0  kernel.softlockup_panic=1 video=1024x768 vt.color=0x07 quiet autoyast=http://openqa.opensuse.org/assets/other/autoyast_opensuse_xen_two_disk.xml
set initrd http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-x86_64-CURRENT/boot/x86_64/loader/initrd
goto editandboot

:tumbleweedS1
set kernel http://download.opensuse.org/tumbleweed/repo/oss/boot/x86_64/loader/linux
set cmdline install=http://download.opensuse.org/tumbleweed/repo/oss plymouth.enable=0 textmode=1 Y2DEBUG=1 console=ttyS1,115200 linuxrc.log=/dev/ttyS1 linuxrc.core=/dev/ttyS1 linuxrc.debug=4,trace reboot_timeout=0 ssh=1 sshpassword=nots3cr3t kernel.softlockup_panic=1 video=1024x768 vt.color=0x07 quiet
set initrd http://download.opensuse.org/tumbleweed/repo/oss/boot/x86_64/loader/initrd
goto editandboot

:tumbleweedS2
set kernel http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-x86_64-CURRENT/boot/x86_64/loader/linux
set cmdline usessh=1 sshpassword=linux network=1 install=http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-i586-x86_64-CURRENT console=ttyS2,115200n8 root=/dev/ram0 initrd=initrd textmode=1
set initrd http://openqa.opensuse.org/assets/repo/openSUSE-Tumbleweed-oss-i586-x86_64-CURRENT/boot/x86_64/loader/initrd
goto editandboot

:memtest64efi
set kernel http://${next-server}/tftpboot/ipxe/images/memtest64.efi
set cmdline
set initrd
goto editandboot

:nbxyz
iseq ${platform} pcbios && chain --autofree http://boot.netboot.xyz/ipxe/netboot.xyz.lkrn ||
iseq ${buildarch} arm64 && chain --autofree http://boot.netboot.xyz/ipxe/netboot.xyz-arm64.efi ||
chain --autofree http://boot.netboot.xyz/ipxe/netboot.xyz.efi

:leapS1aarch64auto
set kernel http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/aarch64/linux
set cmdline network=1 install=http://download.opensuse.org/distribution/leap/15.5/repo/oss/ root=/dev/ram0 initrd=initrd textmode=1 console=tty console=ttyS1,115200 autoyast=https://is.gd/oqaay4 rootpassword=opensuse
set initrd http://download.opensuse.org/distribution/leap/15.5/repo/oss/boot/aarch64/initrd
goto editandboot

:editandboot
echo Selected settings:
echo kernel: ${kernel}
echo cmdline: ${cmdline}
echo initrd: ${initrd}
echo
echo You can modify kernel, cmdline and initrd before boot
echo -n Kernel: ${}
read kernel
echo -n Kernel cmdline: ${}
read cmdline
echo -n Initrd: ${}
read initrd

# clear the screen to prevent lost chars (eg in ipmi openqa tests)
set esc:hex 1b            # ANSI escape character - "^["
set cls ${esc:string}[2J  # ANSI clear screen sequence - "^[[2J"
echo ${cls}
sleep 1 # clearing the screen may take a second

kernel ${kernel} ${cmdline}
initrd ${initrd}
boot

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

# ------------ file end -------------------
